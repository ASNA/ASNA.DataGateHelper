Using System
Using System.Diagnostics
Using ASNA.DataGate.Client
Using ASNA.DataGate.Common
Using ASNA.DataGate.Providers
Using System.Data 
Using System.Reflection 
Using System.Collections.Generic

DclNamespace ASNA.DataGateHelper

BegClass DGFileReader Access(*Public) 
    DclFld DGDB     Type(AdgConnection) 
    DclFld DGFile   Type(FileAdapter)     
    DclFld DGDS     Type(AdgDataSet) 

    // If *False, the DataSet property contains only the current row.
    // If *True, the DataSet property is populated with all rows read.
    DclFld AccumulateRows Type(*Boolean) Access(*Public)  

    DclFld CustomClassAssembly  Type(Assembly) 
    DclFld CustomClassType Type(Type) Access(*Public) Inz(*Nothing)    
    DclFld CustomClassInstance Type(*Object) Access(*Public) 

    DclFld ReadMilliseconds Type(*Integer4) Access(*Public)
    DclFld TotalRowsCounter Type(*Integer4) Access(*Public)
    DclFld RowErrorCounter  Type(*Integer4) Access(*Public)
    DclFld BeginningRecordCount Type(*Integer4) Access(*Public) 

    DclArray FieldNames Type(*String) Rank(1)
    DclArray FieldTypes Type(*String) Rank(1) 

    DclFld BlockingFactor Type(*Integer4) 
    DclFld RecordCount Type(*Integer8) Access(*Public)

    DclFld LibraryName Type(*String)
    DclFld FileName Type(*String) 

    // Declare `AfterRowRead` event.
    DclEvent AfterRowRead
        DclSrParm Sender Type(*Object) 
        DclSrParm e Type(AfterRowReadArgs) 

    DclEvent RowReadError
        DclSrParm Sender Type(*Object) 
        DclSrParm e Type(RowReadErrorArgs) 

    DclEvent BeforeFirstRead
        DclSrParm Sender Type(*Object) 
        DclSrParm e Type(EventArgs) 

    // Constructor with API DB connection only.
    BegConstructor Access(*Public) This(DBConnection, FileOpenAttr.OptimalBlockingFactor)
        DclSrParm DBConnection Type(AdgConnection)
    EndConstructor 

    // Constructor with API DB connection and custom blocking factor.
    BegConstructor Access(*Public) 
        DclSrParm DBConnection Type(AdgConnection)
        DclSrParm BlockingFactor Type(*Integer4) 

        *This.DGDB = DBConnection 

        Init(BlockingFactor)
    EndConstructor 
    
    // Constructor with AVR DB connection only.
    BegConstructor Access(*Public) This(DBConnection, FileOpenAttr.OptimalBlockingFactor)
        DclSrParm DBConnection Type(ASNA.VisualRPG.Runtime.Database) 
    EndConstructor 

    // Constructor with AVR DB connection and custom blocking factor.
    BegConstructor Access(*Public)
        DclSrParm DBConnection Type(ASNA.VisualRPG.Runtime.Database) 
        DclSrParm BlockingFactor Type(*Integer4) 

        *This.DGDB = DBConnection.Connection

        Init(BlockingFactor)
    EndConstructor 

    BegSr Init
        DclSrParm BlockingFactor Type(*Integer4) 

        *This.DGDB.Open()

        // Default is to not accumulate rows in the DataSet property. 
        *This.AccumulateRows = *False 
        *This.BlockingFactor = BlockingFactor 
    EndSr 
  
    // Open file for read-only use.        
    BegSr OpenForRead 
        //DclSrParm DBName Type(*String) 
        DclSrParm Library Type(*String) 
        DclSrParm File Type(*String)

        DclFld Index Type(*Integer4) 
        DclFld FieldType Type(*String) 

        *This.LibraryName = Library
        *THis.FileName = File 

        // Instance file object.
        If Library = "/"
            DGFile = *New FileAdapter(DGDB, File)
        Else
            DGFile = *New FileAdapter(DGDB, String.Format('{0}/{1}', Library, File))
        EndIf 

        // Set read-only with optimal record blocking.
        DGFile.AccessMode = AccessMode.Read
        DGFile.OpenAttributes.BlockingFactor = *This.BlockingFactor 
        // Open a new AdgDataSet. 
        Try 
            DGFile.OpenNewAdgDataSet(*ByRef DGDS)
        Catch ex Type(Exception)
            Throw *New System.ArgumentException(String.Format('An error occurred trying to open {0}/{1}. Additional info: {2}.', Library, File, ex.Message))
        EndTry 

        *This.RecordCount = DGFile.RecordCount

        // Populate FieldNames array.
        *This.FieldNames = *New *String[DGDS.Tables[0].Columns.Count]
        For (Index = 0) To(DGDS.Tables[0].Columns.Count -1)
            *This.FieldNames[Index] = +
                DGDS.Tables[0].Columns[Index].ColumnName.ToString() 
        EndFor

        *This.FieldTypes = *New *String[DGDS.Tables[0].Columns.Count]
        For (Index = 0) To(DGDS.Tables[0].Columns.Count -1)
            FieldType = DGDS.Tables[0].Columns[Index].DataType.ToString().ToLower()
            FieldType = FieldType.Replace('system.', String.Empty) 
            *This.FieldTypes[Index] = FieldType                 
        EndFor
    EndSr

    BegSr CloseReader Access(*Public) 
        *This.DGFile.Close()
    EndSr

    // Read entire file.
    BegFunc ReadEntireFile Access(*Public) Type(DGFileReader) 
        //DclSrParm DBName Type(*String) 
        DclSrParm Library Type(*String) 
        DclSrParm File Type(*String)

        DclFld AfterReadArgs Type(AfterRowReadArgs) 
        DclFld sw Type(StopWatch) New()

        *This.CustomClassAssembly = System.Reflection.Assembly.GetExecutingAssembly()

        sw.Start()
          
        // Open the DataGate file.
        OpenForRead(Library, File) 
        
		*This.BeginningRecordCount = DGFile.RecordCount        
        
        AfterReadArgs = *New AfterRowReadArgs(*Nothing, + 
                                         *This.FieldNames, +
                                         *This.FieldTypes, + 
                                          0, +
                                          DGFile.RecordCount)

        *This.TotalRowsCounter = DGFile.RecordCount 

        BeforeFirstRead(*This, *New System.EventArgs()) 

        // Read all records. 
        DoWhile ReadNextRecord(AfterReadArgs)
            // The AfterRowRead event is raised for each row read.
        EndDo 

        // Close file.
        CloseReader()

        sw.Stop()

        *This.ReadMilliseconds = sw.ElapsedMilliseconds

        LeaveSr *This
    EndFunc

    BegSr PopulateClassInstance 
        DclSrParm DataRow Type(System.Data.DataRow) 

        DclFld FieldValue Type(*Object) 
        DclFld type Type(Type) 
        DclFld prop Type(PropertyInfo) 

        DclArray Props Type(PropertyInfo) Rank(1)

        type = *This.CustomClassInstance.GetType()
        Props = type.GetProperties()

        ForEach FieldName Type(*String) Collection(*This.FieldNames)
            FieldValue = DataRow[FieldName] 
            type = *This.CustomClassInstance.GetType()

            // Gotta iterate properties because GetProperty method 
            // is case sensitive.
            ForEach prop Collection(type.GetProperties())
                If prop.Name.ToLower() = FieldName.ToLower()
                    prop.SetValue(*This.CustomClassInstance, FieldValue, *Nothing)
                    Leave 
                EndIf 
            EndFor 
         EndFor 
    EndSr 

    // Read next available record. 
    // Return *True if record read; otherwise return *False.
    BegFunc ReadNextRecord Type(*Boolean)       
        DclSrParm EventArgs Type(AfterRowReadArgs) 

        DclFld RowErrorArgs Type(RowReadErrorArgs)

        If EventArgs.CancelRead
            LeaveSr *False 
        EndIf 

        // ReadSequential doesn't know about EOF--it throws a DataGate
        // exception when there are no more records to read. 
        // This function puts an EOF-friendly wrapper around ReadSequential.

        // Don't accumulate rows in the DataSet.
        If NOT *This.AccumulateRows
            DGDS.Tables[0].Clear()
        EndIF 
            
        Try
            // Read a record.
            DGFile.ReadSequential(DGDS, ReadSequentialMode.Next, LockRequest.NoLock)
            EventArgs.CurrentRowCounter = EventArgs.CurrentRowCounter + 1
            EventArgs.DataRow = DGDS.ActiveRow

            If *This.CustomClassType <> *Nothing
                *This.CustomClassInstance = Activator.CreateInstance(*This.CustomClassType) 
                *This.PopulateClassInstance(DGDS.ActiveRow)
                EventArgs.CustomClassInstance = *This.CustomClassInstance 
            EndIf 

            // Raise AfterRowRead event.
            AfterRowRead(*This, EventArgs)

            LeaveSr *True
        Catch DgEx Type(dgException) 
            // If EOF, swallow exception and return *False, 
            // which indicates EOF has been reached.
            If (DgEx.Error = dgErrorNumber.dgEaEOF)
                LeaveSr *False 
            Else
                // Otherwise rethrow error exception. 
                Throw DgEx           
            EndIf

        Catch Ex Type(Exception)
            If ex.Message.StartsWith("Invalid data was encountered")
                EventArgs.CurrentRowCounter = EventArgs.CurrentRowCounter + 1
                *This.RowErrorCounter = *This.RowErrorCounter + 1
                RowErrorArgs = *New RowReadErrorArgs(*This.FileName, EventArgs.CurrentRowCounter, ex.Message)
                RowReadError(*This, RowErrorArgs)
                LeaveSr *True
            Else
                Throw Ex           
            EndIf 
        EndTry   
    EndFunc

    // This routine might come in handy if you want to use a database name
    // in a Windows file name.
    BegFunc NormalizeDatabaseName Type(*String) Shared(*Yes) Access(*Public) 
        DclSrParm DatabaseName Type(*String)

        DatabaseName = DatabaseName.Replace('/', '_').ToLower()
        DatabaseName = DatabaseName.Replace(' ', '_').ToLower()
        DatabaseName = DatabaseName.Replace('*public', 'public')

        LeaveSr DatabaseName
    EndFunc 

    BegFunc Mod Type(*Boolean) Shared(*Yes) Access(*Public) 
        // Return the remainder of division.
        DclSrParm Dividend *Integer4 
        DclSrParm Divisor  *Integer4 

        DclFld Remainder *Integer4 

        System.Math.DivRem(Dividend, Divisor, *ByRef Remainder) 

        Leavesr Remainder = 0 
    EndFunc

    BegFunc RemoveLastCharacter Type(*String) Shared(*Yes) Access(*Public) 
        DclSrParm InputString Type(*String) 

        LeaveSr InputString.Remove(InputString.Length - 1, 1)
    EndFunc 

EndClass

BegClass RowReadErrorArgs Extends(System.EventArgs) Access(*Public) 
    DclFld FileName Type(*String) Access(*Public) 
    DclFld RowNumber Type(*Integer4) Access(*Public) 
    DclFld ErrorMessage Type(*String) Access(*Public)

    BegConstructor Access(*Public)
        DclSrParm FileName TYpe(*String) 
        DclSrParm RowNumber Type(*Integer4)
        DclSrParm ErrorMessage Type(*String) 

        *THis.FileName = FileName 
        *This.RowNumber = RowNumber
        *This.ErrorMessage = ErrorMessage
    EndConstructor 
EndClass

// Provides the `e` argument for the AfterRowRead event.
BegClass AfterRowReadArgs Extends(System.EventArgs) Access(*Public)
    DclFld DataRow Type(System.Data.DataRow) Access(*Public)  
    DclArray FieldNames Type(*String) Rank(1) Access(*Public)
    DclArray FieldTypes Type(*String) Rank(1) Access(*Public)
    DclFld CurrentRowCounter Type(*Integer4) Access(*Public)
    DclFld TotalRowsCounter Type(*Integer8) Access(*Public)
    DclFld CancelRead Type(*Boolean) Access(*Public) 
    DclFld CustomClassInstance Type(*Object) Access(*Public) 
    
    BegConstructor Access(*Public) 
        DclSrParm DataRow  Type(System.Data.DataRow) 
        DclSrParm FieldNames Type(*String) Rank(1)
        DclSrParm FieldTypes Type(*String) Rank(1)
        DclSrParm CurrentRowCounter Type(*Integer4) 
        DclSrParm TotalRowsCounter Type(*Integer8) 
        
        *This.DataRow = DataRow
        *This.FieldNames = FieldNames
        *This.FieldTypes = FieldTypes
        *This.CurrentRowCounter = CurrentRowCounter
        *This.TotalRowsCounter = TotalRowsCounter  
        *This.CancelRead = *False 
    EndConstructor
EndClass
