Using System
Using System.Text
Using System.Text.RegularExpressions
Using System.Collections.Generic
Using System.Diagnostics

DclNamespace ASNA.DataGateHelper

BegClass PagedData
    DclDB DGDB  DBName('*Public/Leyland') 

    DclConst SUCCESS Value(0) 

    DclFld dgfr Type(ASNA.DataGateHelper.DGFileReader) WithEvents(*Yes)

    DclProp MillisecondsToCreateWorkFile Type(*Integer4) Access(*Public) SetAccess(*Private)
    DclProp MillisecondsToReadWorkFile Type(*Integer4) Access(*Public) SetAccess(*Private)
    DclProp MoreRecords Type(*Boolean) Access(*Public) SetAccess(*Private) 
    DclProp RowsRead Type(*Integer4) Access(*Public) SetAccess(*Private)  

    DclFld ReturnedSQLCode Type(*String) 
    DclFld ReturnedSQLState Type(*String)  
    DclFld MessageText Type(*String) 

    DclFld LibraryName Type(*String)   
    DclFld PageSize Type(*Integer4) 

    DclFld SQLClauses Type(Dictionary (*Of *String, *String)) New()

       
    DclFld iSql Type(IBMiSqlPage)  
    
    BegConstructor Access(*Public) 
        DclSrParm DGDB Type(ASNA.VisualRPG.Runtime.DataBase) 
        DclSrParm LibraryName Type(*String) 
        DclSrParm ProgramLibrary Type(*String) 
        DclSrParm RPGProgramToCall Type(*String)
        DclSrParm PageSize Type(*Integer4) 
        DclSrParm CustomClassType Type(Type) 

        *This.DGDB = DGDB 
        Connect DGDB 

        *This.LibraryName = LibraryName 
        *This.PageSize = PageSize 

        *This.iSql = *New IBMiSqlPage(DGDB, ProgramLibrary, RPGProgramToCall) 
        dgfr = *New ASNA.DataGateHelper.DGFileReader(DGDB)
        dgfr.CustomClassType = CustomClassType
    EndConstructor 

    DclEvent AfterRowRead
        DclSrParm Sender Type(*Object) 
        DclSrParm e Type(ASNA.DataGateHelper.AfterRowReadArgs)

    BegSr WriteThenReadTempFile Access(*Public) 
        DclSrParm Page Type(*Integer4) 

        DclFld Sql  Type(*String) 

        DclFld UniqueObjectId Type(*String) 

        Sql = *This.CreateSQL()

        UniqueObjectId = Utility.GenerateUniqueObjectId() 

        WriteTempFile(UniqueObjectId, Page, Sql) 
        ReadTempFile(UniqueObjectId) 
    EndSr

    BegSr WriteTempFile 
        DclSrParm UniqueObjectId Type(*String) 
        DclSrParm Page Type(*Integer4) 
        DclSrParm Sql Type(*String) 

        DclFld ErrorCode Type(*Integer4) 
        DclFld Offset Type(*Integer4) 
        DclFld sw Type(StopWatch) New() 

        Offset = (*This.PageSize * Page) - *This.PageSize
        Sql = Sql.Replace('{libraryName}', *This.LibraryName)
        Sql = Sql.Replace('{uniqueObjectId}', UniqueObjectId)
        
        // Read one more row than requested. The presence of the +1 row will 
        // later be used to determine if more records are avaiable.
        Sql = Sql.Replace('{limit}', (*This.PageSize + 1).ToString())
        Sql = Sql.Replace('{offset}', Offset.ToString())

        sw.Start()
        // Call RPG program object to create temp work file with SQL.
        ErrorCode = ISql.Call(Sql)
        sw.Stop() 
        *This.MillisecondsToCreateWorkFile =  sw.ElapsedMilliseconds

        *This.ReturnedSQLCode = ISql.ReturnedSQLCode
        *This.ReturnedSQLState = ISql.ReturnedSQLState 
        *This.MessageText = ISql.MessageText.Trim() 
        
        If ErrorCode <> SUCCESS
            Throw *New ApplicationException(String.Format("ERROR calling IBM i program. SQLCode={0} SQLState={1} Message={2}", +
                                                          *This.ReturnedSQLCode, *This.ReturnedSQLState, *This.MessageText)) 
            LeaveSr
        EndIf

        *This.RowsRead = iSql.RowsCount
     EndSr

    BegSr ReadTempFile
        DclSrParm UniqueObjectId Type(*String) 

        DclFld sw Type(StopWatch) New() 
        
        sw.Start()

        // Read the entire temp work file raising the 
        // OnAfterRowRead event for each record read. 
        dgfr.ReadEntireFile(*This.LibraryName, UniqueObjectId)

        sw.Stop() 
        *This.MillisecondsToReadWorkFile = sw.ElapsedMilliseconds
        *This.MoreRecords = NOT (*This.RowsRead <= *This.PageSize)
    EndSr 

    BegSr OnAfterRowRead Event(dgfr.AfterRowRead) 
        DclSrParm Sender Type(*Object)
        DclSrParm e Type(ASNA.DataGateHelper.AfterRowReadArgs) 

        If e.CurrentRowCounter <= *This.PageSize
            *This.AfterRowRead(*This, e)
        EndIf 
    EndSr

    BegSr AddSQLSelect Access(*Public) 
        DclSrParm SelectClause Type(*String) 

        SelectClause = RegEx.Replace(SelectClause, "^\s*select", String.Empty, RegexOptions.IgnoreCase) 
        *This.SQLClauses.Add('select', SelectClause) 
    EndSr

    BegSr AddSQLFrom Access(*Public) 
        DclSrParm FromClause Type(*String) 

        FromClause = RegEx.Replace(FromClause, "^\s*from", String.Empty, RegexOptions.IgnoreCase) 
        *This.SQLClauses.Add('from', FromClause) 
    EndSr

    BegSr AddSQLWhere Access(*Public) 
        DclSrParm WhereClause Type(*String) 

        WhereClause = RegEx.Replace(WhereClause, "^\s*where", String.Empty, RegexOptions.IgnoreCase) 
        *This.SQLClauses.Add('where', WhereClause) 
    EndSr

    BegSr AddSQLOrderBy Access(*Public) 
        DclSrParm OrderByClause Type(*String) 

        OrderByClause = RegEx.Replace(OrderByClause, "^\s*order\s+by", String.Empty, RegexOptions.IgnoreCase) 
        *This.SQLClauses.Add('orderby', OrderByClause) 
    EndSr

    BegFunc CreateSQL Type(*String)

        DclFld sb Type(StringBuilder) New()
                                                 
        sb.AppendLine('CREATE TABLE {libraryName}/{uniqueObjectId} as (' ) 
        sb.AppendLine('WITH result AS ( ')

        If *This.SQLClauses.ContainsKey('select') 
            sb.AppendLine(String.Format("SELECT {0} ", *This.SQLClauses['select']))
        Else 
            sb.AppendLine("SELECT * ") 
        EndIf 

        If *This.SQLClauses.ContainsKey('from') 
            sb.AppendLine(String.Format("FROM {0} ", *This.SQLClauses['from']))
        Else 
            Throw *New ArgumentException("'from' key required to create SQL")
        EndIf 
         
        If *This.SQLClauses.ContainsKey('where') 
            sb.AppendLine(String.Format("WHERE {0} ", *This.SQLClauses['where']))
        EndIf 

        If *This.SQLClauses.ContainsKey('orderby') 
            sb.AppendLine(String.Format("ORDER BY {0} ", *This.SQLClauses['orderby']))
        Else 
            Throw *New ArgumentException("'orderby' key required to create SQL")
        EndIf 

        sb.AppendLine('LIMIT {limit} ')
        sb.AppendLine('OFFSET {offset} ')

        sb.AppendLine(') SELECT * FROM result ) WITH DATA')
        LeaveSr sb.ToString()
    EndFunc

EndClass
