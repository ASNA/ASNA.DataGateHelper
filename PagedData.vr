Using System
Using System.Text
Using System.Collections.Generic
Using System.Diagnostics

DclNamespace ASNA.DataGateHelper

BegClass PagedData
    DclDB DGDB  DBName('*Public/Leyland') 

    DclConst SUCCESS Value(0) 

    DclFld dgfr Type(ASNA.DataGateHelper.DGFileReader) WithEvents(*Yes)

    DclFld MoreRecords Type(*Boolean) Access(*Public) 
    DclFld RowsRead Type(*Integer4) Access(*Public)  
    DclFld MillisecondsToCreateWorkFile Type(*Integer4) Access(*Public)
    DclFld MillisecondsToReadWorkFile Type(*Integer4) Access(*Public)

    DclFld ReturnedSQLCode Type(*String) 
    DclFld ReturnedSQLState Type(*String)  
    DclFld MessageText Type(*String) 


    DclFld LibraryName Type(*String)   
    DclFld PageSize Type(*Integer4) 
       
    DclFld iSql Type(IBMiSqlPage)  
    
    BegConstructor Access(*Public) 
        DclSrParm DGDB Type(ASNA.VisualRPG.Runtime.DataBase) 
        DclSrParm LibraryName Type(*String) 
        DclSrParm ProgramLibrary Type(*String) 
        DclSrParm RPGProgramToCall Type(*String)
        DclSrParm CustomClassType Type(Type)    // *TypeOf(Customer) 

        *This.DGDB = DGDB 
        Connect DGDB 

        *This.LibraryName = LibraryName 

        *This.iSql = *New IBMiSqlPage(DGDB, ProgramLibrary, RPGProgramToCall) 
        dgfr = *New ASNA.DataGateHelper.DGFileReader(DGDB)
        dgfr.CustomClassType = CustomClassType
    EndConstructor 

    DclEvent AfterRowRead
        DclSrParm Sender Type(*Object) 
        DclSrParm e Type(ASNA.DataGateHelper.AfterRowReadArgs)

    BegSr WriteThenReadTempFile Access(*Public) 
        DclSrParm PageSize Type(*Integer4) 
        DclSrParm Page Type(*Integer4) 
        DclSrParm Sql Type(*String) 

        DclFld UniqueObjectId Type(*String) 

        *This.PageSize = PageSize 

        UniqueObjectId = Utility.GenerateUniqueObjectId() 

        WriteTempFile(UniqueObjectId, PageSize, Page, Sql) 
        ReadTempFile(UniqueObjectId, PageSize) 
    EndSr

    BegSr WriteTempFile Access(*Public)
        DclSrParm UniqueObjectId Type(*String) 
        DclSrParm PageSize Type(*Integer4) 
        DclSrParm Page Type(*Integer4) 
        DclSrParm Sql Type(*String) 

        DclFld ErrorCode Type(*Integer4) 
        DclFld Offset Type(*Integer4) 
        DclFld sw Type(StopWatch) New() 

        Offset = (PageSize * Page) - PageSize
        Sql = Sql.Replace('{libraryName}', *This.LibraryName)
        Sql = Sql.Replace('{uniqueObjectId}', UniqueObjectId)
        
        // Read one more row than requested. The presence of the +1 row will 
        // later be used to determine if more records are avaiable.
        Sql = Sql.Replace('{limit}', (PageSize + 1).ToString())
        Sql = Sql.Replace('{offset}', Offset.ToString())

        sw.Start()
        // Call RPG program object to create temp work file with SQL.
        ErrorCode = ISql.Call(Sql)
        sw.Stop() 
        *This.MillisecondsToCreateWorkFile =  sw.ElapsedMilliseconds

        *This.ReturnedSQLCode = ISql.ReturnedSQLCode
        *This.ReturnedSQLState = ISql.ReturnedSQLState 
        *This.MessageText = ISql.MessageText.Trim() 
        
        If ErrorCode <> SUCCESS
            Throw *New ApplicationException(String.Format("ERROR calling IBM i program. SQLCode={0} SQLState={1} Message={2}", +
                                                          *This.ReturnedSQLCode, *This.ReturnedSQLState, *This.MessageText)) 
            LeaveSr
        EndIf

        *This.RowsRead = iSql.RowsCount
     EndSr

    BegSr ReadTempFile Access(*Public)
        DclSrParm UniqueObjectId Type(*String) 
        DclSrParm PageSize Type(*Integer4) 

        DclFld sw Type(StopWatch) New() 
        
        sw.Start()

        // Read the entire temp work file raising the 
        // OnAfterRowRead event for each record read. 
        dgfr.ReadEntireFile(*This.LibraryName, UniqueObjectId)

        sw.Stop() 
        *This.MillisecondsToReadWorkFile = sw.ElapsedMilliseconds

        *This.MoreRecords = NOT (*This.RowsRead <= PageSize)
    EndSr 

    BegSr OnAfterRowRead Event(dgfr.AfterRowRead) 
        DclSrParm Sender Type(*Object)
        DclSrParm e Type(ASNA.DataGateHelper.AfterRowReadArgs) 

        If e.CurrentRowCounter <= *This.PageSize
            *This.AfterRowRead(*This, e)
        EndIf 
    EndSr

    BegFunc CreateSQL Type(*String) Access(*Public) Shared(*Yes)         
        DclSrParm QueryArgs Type(Dictionary (*Of *String, *String)) 

        DclFld sb Type(StringBuilder) New()
                                                 
        sb.AppendLine('CREATE TABLE {libraryName}/{uniqueObjectId} as (' ) 
        sb.AppendLine('WITH result AS ( ')

        If QueryArgs.ContainsKey('select') 
            sb.AppendLine(String.Format("SELECT {0} ", QueryArgs['select']))
        Else 
            sb.AppendLine("SELECT * ") 
        EndIf 

        If QueryArgs.ContainsKey('from') 
            sb.AppendLine(String.Format("FROM {0} ", QueryArgs['from']))
        Else 
            Throw *New ArgumentException("'from' key required to create SQL")
        EndIf 
         
        If QueryArgs.ContainsKey('where') 
            sb.AppendLine(String.Format("WHERE {0} ", QueryArgs['where']))
        EndIf 

        If QueryArgs.ContainsKey('orderby') 
            sb.AppendLine(String.Format("ORDER BY {0} ", QueryArgs['orderby']))
        Else 
            Throw *New ArgumentException("'orderby' key required to create SQL")
        EndIf 

        sb.AppendLine('LIMIT {limit} ')
        sb.AppendLine('OFFSET {offset} ')

        sb.AppendLine(') SELECT * FROM result ) WITH DATA')
        LeaveSr sb.ToString()
    EndFunc

EndClass
